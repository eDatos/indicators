import "classpath:/btdesign/export/metamac-core-common-entity.btdesign"
import "classpath:/btdesign/metamac-core-common-dtos.btdesign"

Application Indicators {
    basePackage=es.gobcan.istac.indicators
    
    Module core {
		basePackage=es.gobcan.istac.indicators.core
		
		// ----------------------------------------------------------------
		// 							SERVICES
		// ----------------------------------------------------------------     
		
		"Provides access to indicators systems"
    	Service IndicatorsSystemsService {
    		> @IndicatorsSystemRepository
    		> @IndicatorsSystemVersionRepository
    		> @DimensionRepository
    		> @IndicatorInstanceRepository
    		> @ElementLevelRepository
    		
	    	// DIMENSIONS
//	    	updateDimension(@DimensionDto dimensionDto) throws MetamacException;
	    	
	    	// INDICATORS INSTANCES	    	
//	    	updateIndicatorInstance(@IndicatorInstanceDto indicatorInstanceDto) throws MetamacException;
    		
    		// Indicators system
    		@IndicatorsSystemVersion createIndicatorsSystem(@IndicatorsSystem indicatorsSystem, @IndicatorsSystemVersion indicatorsSystemVersion) throws MetamacException;
//	    	updateIndicatorsSystem(@IndicatorsSystemDto indicatorsSystemDto) throws MetamacException;
	    	@IndicatorsSystem retrieveIndicatorsSystemBorrar(String uuid) throws MetamacException;
	    	@IndicatorsSystemVersion retrieveIndicatorsSystem(String uuid, String versionNumber) throws MetamacException;
	    	@IndicatorsSystemVersion retrieveIndicatorsSystemPublished(String uuid) throws MetamacException;
	    	@IndicatorsSystemVersion retrieveIndicatorsSystemByCode(String code, String versionNumber) throws MetamacException;
	    	@IndicatorsSystemVersion retrieveIndicatorsSystemPublishedByCode(String code) throws MetamacException;
	    	List<@ElementLevel> retrieveIndicatorsSystemStructure(String uuid, String versionNumber) throws MetamacException;
	    	@IndicatorsSystem updateIndicatorsSystem(@IndicatorsSystem indicatorsSystem) throws MetamacException;
	    	@IndicatorsSystemVersion updateIndicatorsSystemVersion(@IndicatorsSystemVersion indicatorsSystemVersion) throws MetamacException;
	    	deleteIndicatorsSystem(String uuid) throws MetamacException;
	    	List<@IndicatorsSystem> findIndicatorsSystems(String code) throws MetamacException;
	    	List<@IndicatorsSystemVersion> findIndicatorsSystemVersions(String uriGopestat, @IndicatorsSystemStateEnum state) throws MetamacException;
	    	List<@IndicatorsSystemVersion> findIndicatorsSystems() throws MetamacException;
	    	List<@IndicatorsSystemVersion> findIndicatorsSystemsPublished() throws MetamacException;
	    	sendIndicatorsSystemToProductionValidation(String uuid) throws MetamacException;
	    	sendIndicatorsSystemToDiffusionValidation(String uuid) throws MetamacException;
	    	rejectIndicatorsSystemValidation(String uuid) throws MetamacException;
	    	publishIndicatorsSystem(String uuid) throws MetamacException;	    		    	
	    	archiveIndicatorsSystem(String uuid) throws MetamacException;	    		    	
	    	@IndicatorsSystemVersion versioningIndicatorsSystem(String uuid, @VersiontTypeEnum versionType) throws MetamacException;
	    	
	    	// Elements levels
	    	@ElementLevel updateElementLevel(@ElementLevel elementLevel) throws MetamacException;
	    	
	    	// Dimensions
	    	@Dimension createDimension(String indicatorsSystemUuid, @Dimension dimension) throws MetamacException;
	    	@Dimension updateDimension(@Dimension dimension) throws MetamacException;
	    	updateDimensionLocation(String uuid, String parentTargetUuid, Long orderInLevel) throws MetamacException;
	    	@Dimension retrieveDimension(String uuid) throws MetamacException;
	    	deleteDimension(String uuid) throws MetamacException;
	    	List<@Dimension> findDimensions(String indicatorsSystemUuid, String indicatorsSystemVersionNumber) throws MetamacException;
	    	
	    	// Indicators instances
	    	@IndicatorInstance createIndicatorInstance(String indicatorsSystemUuid, @IndicatorInstance indicatorInstance) throws MetamacException;
	    	@IndicatorInstance updateIndicatorInstance(@IndicatorInstance indicatorInstance) throws MetamacException;
	    	updateIndicatorInstanceLocation(String uuid, String parentTargetUuid, Long orderInLevel) throws MetamacException;
	    	@IndicatorInstance retrieveIndicatorInstance(String uuid) throws MetamacException;
	    	deleteIndicatorInstance(String uuid) throws MetamacException;
	    	List<@IndicatorInstance> findIndicatorsInstances(String indicatorsSystemUuid, String indicatorsSystemVersionNumber) throws MetamacException;
    	}
    	
    	"Provides access to indicators"
    	Service IndicatorsService {
    		> @IndicatorRepository
    		> @IndicatorVersionRepository
    		> @DataSourceRepository
    		
    		// Indicator
    		@IndicatorVersion createIndicator(@Indicator indicator, @IndicatorVersion indicatorVersion) throws MetamacException;
	    	@Indicator retrieveIndicatorBorrar(String uuid) throws MetamacException;
	    	@IndicatorVersion retrieveIndicator(String uuid, String versionNumber) throws MetamacException;
	    	@IndicatorVersion retrieveIndicatorPublished(String uuid) throws MetamacException;
	    	@IndicatorVersion retrieveIndicatorByCode(String code, String versionNumber) throws MetamacException;
	    	@IndicatorVersion retrieveIndicatorPublishedByCode(String code) throws MetamacException;
	    	updateIndicator(@Indicator indicator) throws MetamacException;
	    	updateIndicatorVersion(@IndicatorVersion indicatorVersion) throws MetamacException;
	    	deleteIndicator(String uuid) throws MetamacException;
	    	List<@Indicator> findIndicators(String code) throws MetamacException;
	    	List<@IndicatorVersion> findIndicatorsVersions(@IndicatorStateEnum state) throws MetamacException;
	    	List<@IndicatorVersion> findIndicators() throws MetamacException;
	    	List<@IndicatorVersion> findIndicatorsPublished() throws MetamacException;
	    	sendIndicatorToProductionValidation(String uuid) throws MetamacException;
	    	sendIndicatorToDiffusionValidation(String uuid) throws MetamacException;
	    	rejectIndicatorValidation(String uuid) throws MetamacException;
	    	publishIndicator(String uuid) throws MetamacException;	    		    	
	    	archiveIndicator(String uuid) throws MetamacException;	    		    	
	    	@IndicatorVersion versioningIndicator(String uuid, @VersiontTypeEnum versionType) throws MetamacException;
	    	
	    	// Data sources
	    	@DataSource createDataSource(String indicatorUuid, @DataSource dataSource) throws MetamacException;
	    	@DataSource updateDataSource(@DataSource dataSource) throws MetamacException;
	    	@DataSource retrieveDataSource(String uuid) throws MetamacException;
	    	deleteDataSource(String uuid) throws MetamacException;
	    	List<@DataSource> findDataSources(String indicatorUuid, String indicatorVersionNumber) throws MetamacException;	    	
	    	
//	    	updateIndicator(@IndicatorDto indicatorDto) throws MetamacException;
//	    	
//	    	updateDataSource(@DataSourceDto dataSourceDto) throws MetamacException;
    	}   	
	    
	    "Provides access to indicators, indicators systems..."
    	Service IndicatorsServiceFacade {
			> @IndicatorsSystemsService
    		> @IndicatorsService
	    	
	    	// INDICATORS SYSTEMS
	    	"Creates an indicators system"
	    	@IndicatorsSystemDto createIndicatorsSystem(@IndicatorsSystemDto indicatorsSystemDto) throws MetamacException;
	    	"Updates metadata of an indicators system. This version can not be published or archived"
	    	updateIndicatorsSystem(@IndicatorsSystemDto indicatorsSystemDto) throws MetamacException;
	    	"Retrieves an indicators system. If versionNumber is not provided, retrieves last version"
	    	@IndicatorsSystemDto retrieveIndicatorsSystem(String uuid, String versionNumber) throws MetamacException;
	    	"Retrieves an indicators system published"
	    	@IndicatorsSystemDto retrieveIndicatorsSystemPublished(String uuid) throws MetamacException;
	    	"Retrieves an indicators system by code. If versionNumber is not provided, retrieves last version"
	    	@IndicatorsSystemDto retrieveIndicatorsSystemByCode(String code, String versionNumber) throws MetamacException;
	    	"Retrieves an indicators system by code. Retrieves diffusion version"
	    	@IndicatorsSystemDto retrieveIndicatorsSystemPublishedByCode(String code) throws MetamacException;
	    	"Deletes a version of an indicators system. Version to remove must be not published nor archived"
	    	deleteIndicatorsSystem(String uuid) throws MetamacException;
	    	"Sends indicators system to production validation"
	    	sendIndicatorsSystemToProductionValidation(String uuid) throws MetamacException;
	    	"Sends indicators system to diffusion validation"
	    	sendIndicatorsSystemToDiffusionValidation(String uuid) throws MetamacException;
	    	"Rejects validation of indicators system"
	    	rejectIndicatorsSystemValidation(String uuid) throws MetamacException;
	    	"Publishes indicators system"
	    	publishIndicatorsSystem(String uuid) throws MetamacException;	    		    	
	    	"Archives indicators system"
	    	archiveIndicatorsSystem(String uuid) throws MetamacException;	    		    	
	    	"Creates a version on draft of an indicators system in diffusion. Returns new version created"
	    	@IndicatorsSystemDto versioningIndicatorsSystem(String uuid, @VersiontTypeEnum versionType) throws MetamacException;
	    	"Finds indicators systems. Retrieves last versions"
	    	List<@IndicatorsSystemDto> findIndicatorsSystems() throws MetamacException;
	    	"Finds published indicators systems"
	    	List<@IndicatorsSystemDto> findIndicatorsSystemsPublished() throws MetamacException;
	    	"Retrieves indicators system structure: dimensions and indicators instances by levels. If versionNumber is not provided, retrieves last version"
	    	@IndicatorsSystemStructureDto retrieveIndicatorsSystemStructure(String uuid, String versionNumber) throws MetamacException;
	    	
	    	// DIMENSIONS
	    	"Creates a dimension"
	    	@DimensionDto createDimension(String indicatorsSystemUuid, @DimensionDto dimensionDto) throws MetamacException;
	    	"Updates metadata of dimension"
	    	updateDimension(@DimensionDto dimensionDto) throws MetamacException;
	    	"Updates the location of dimension"
	    	updateDimensionLocation(String uuid, String parentTargetUuid, Long orderInLevel) throws MetamacException;
	    	"Retrieves dimension"
	    	@DimensionDto retrieveDimension(String uuid) throws MetamacException;
	    	"Deletes dimension"
	    	deleteDimension(String uuid) throws MetamacException;
	    	"Retrieves all dimensions of a indicators system regardless of the level"
	    	List<@DimensionDto> findDimensions(String indicatorsSystemUuid, String indicatorsSystemVersionNumber) throws MetamacException;
	    	
	    	// INDICATORS INSTANCES	    	
	    	"Creates a indicator instance"
	    	@IndicatorInstanceDto createIndicatorInstance(String indicatorsSystemUuid, @IndicatorInstanceDto indicatorInstanceDto) throws MetamacException;
	    	"Updates metadata of indicator instance"
	    	updateIndicatorInstance(@IndicatorInstanceDto indicatorInstanceDto) throws MetamacException;
	    	"Updates the location of indicator instance"
	    	updateIndicatorInstanceLocation(String uuid, String parentTargetUuid, Long orderInLevel) throws MetamacException;
	    	"Retrieves indicator instance"
	    	@IndicatorInstanceDto retrieveIndicatorInstance(String uuid) throws MetamacException;
	    	"Deletes indicator instance"
	    	deleteIndicatorInstance(String uuid) throws MetamacException;
	    	"Retrieves all indicators instances of a indicators system regardless of the level"
	    	List<@IndicatorInstanceDto> findIndicatorsInstances(String indicatorsSystemUuid, String indicatorsSystemVersionNumber) throws MetamacException;
	    	
	    	// INDICATORS
	    	"Creates an indicator"
	    	@IndicatorDto createIndicator(@IndicatorDto indicatorDto) throws MetamacException;
	    	"Updates metadata of an indicator. This version can not be published or archived"
	    	updateIndicator(@IndicatorDto indicatorDto) throws MetamacException;
	    	"Retrieves an indicator. If versionNumber is not provided, retrieves last version"
	    	@IndicatorDto retrieveIndicator(String uuid, String versionNumber) throws MetamacException;
	    	"Retrieves an indicator published"
	    	@IndicatorDto retrieveIndicatorPublished(String uuid) throws MetamacException;
	    	"Retrieves an indicator by code. If versionNumber is not provided, retrieves last version"
	    	@IndicatorDto retrieveIndicatorByCode(String code, String versionNumber) throws MetamacException;
	    	"Retrieves an indicator by code. Retrieves diffusion version"
	    	@IndicatorDto retrieveIndicatorPublishedByCode(String code) throws MetamacException;
	    	"Deletes a version of an indicator. Version to remove must be not published nor archived"
	    	deleteIndicator(String uuid) throws MetamacException;
	    	"Sends indicator to production validation"
	    	sendIndicatorToProductionValidation(String uuid) throws MetamacException;
	    	"Sends indicator to diffusion validation"
	    	sendIndicatorToDiffusionValidation(String uuid) throws MetamacException;
	    	"Rejects validation of indicator"
	    	rejectIndicatorValidation(String uuid) throws MetamacException;
	    	"Publishes indicator"
	    	publishIndicator(String uuid) throws MetamacException;	    		    	
	    	"Archives indicator"
	    	archiveIndicator(String uuid) throws MetamacException;	    		    	
	    	"Creates a version on draft of an indicator in diffusion. Returns new version created"
	    	@IndicatorDto versioningIndicator(String uuid, @VersiontTypeEnum versionType) throws MetamacException;
	    	"Finds indicators. Retrieves last versions"
	    	List<@IndicatorDto> findIndicators() throws MetamacException;
	    	"Finds published indicators"
	    	List<@IndicatorDto> findIndicatorsPublished() throws MetamacException;
	    	
	    	// DATASOURCES
	    	"Creates a data source"
	    	@DataSourceDto createDataSource(String indicatorUuid, @DataSourceDto dataSourceDto) throws MetamacException;
	    	"Updates metadata of data source"
	    	updateDataSource(@DataSourceDto dataSourceDto) throws MetamacException;
	    	"Retrieves data source"
	    	@DataSourceDto retrieveDataSource(String uuid) throws MetamacException;
	    	"Deletes data source"
	    	deleteDataSource(String uuid) throws MetamacException;
	    	"Find data sources"
	    	List<@DataSourceDto> findDataSources(String indicatorUuid, String indicatorVersionNumber) throws MetamacException;	    	
	    }

	    // ----------------------------------------------------------------
		// 							ENTITIES
		// ----------------------------------------------------------------        	
		"Indicators system entity"
		Entity IndicatorsSystem {
			databaseTable="TBL_INDICATORS_SYSTEMS"
			hint="idSequence=INDICATORS_SYSTEMS"
			gap
			
			"Semantic identifier of statistic operation. Non modifiable"
       		String code not nullable;
		
			// Can not do an OneToOne relation with IndicatorsSystemVersion because this produces a CyclicDependency with IndicatorsSystem
			"Version in production"
            - @IndicatorsSystemVersionInformation productionVersion nullable databaseColumn="PRODUCTION";
            "Version in diffusion"
            - @IndicatorsSystemVersionInformation diffusionVersion nullable databaseColumn="DIFFUSION";
			"All versions"
			- Bag<@IndicatorsSystemVersion> versions cascade="all-delete-orphan" fetch="lazy" inverse <-> indicatorsSystem orderby="versionNumber asc";
			
			
			Repository IndicatorsSystemRepository {
		   		@IndicatorsSystem save(@IndicatorsSystem entity);
		   		delete;
            	protected List<@IndicatorsSystem> findByQuery;
            	protected List<@IndicatorsSystem> findByQuery(String query, Map<String, Object> parameters, int maxResult);
            	@IndicatorsSystem retrieveIndicatorsSystem(String uuid);
            	List<@IndicatorsSystem> findIndicatorsSystems(String code);
			}
		} 
		
		"Version of indicators system entity"
		Entity IndicatorsSystemVersion {
			databaseTable="TBL_INDIC_SYSTEMS_VERSIONS"
			hint="idSequence=INDIC_SYSTEMS_VERSIONS"
			
			"State"
			- @IndicatorsSystemStateEnum state not nullable;
			String versionNumber not nullable length="10";
       		"Uri of statistic operation in Gopestat"
       		String uriGopestat nullable;
       		"Title of statistic operation"
       		- @InternationalString title cascade="all" databaseColumn="TITLE_FK";
       		"Acronym of statistic operation"
       		- @InternationalString acronym cascade="all" nullable databaseColumn="ACRONYM_FK";
       		"Objetive of statistic operation"
       		- @InternationalString objetive cascade="all" nullable databaseColumn="OBJETIVE_FK";
       		"Description of statistic operation"
       		- @InternationalString description cascade="all" nullable databaseColumn="DESCRIPTION_FK";
       		
			"Date when the indicators system was sended to production validation"
			DateTimeTZ productionValidationDate nullable;
			"User who sended to production validation"
			String productionValidationUser nullable;
			"Date when the indicators system was sended to diffusion validation"
			DateTimeTZ diffusionValidationDate nullable;
			"User who sended to diffusion validation"
			String diffusionValidationUser nullable;
			"Date when the indicators system was published"
			DateTimeTZ publicationDate nullable;
			"User who published"
			String publicationUser nullable;
			"Date when the indicators system was archived"
			DateTimeTZ archiveDate nullable;
			"User who archived"
			String archiveUser nullable;
					
			// Relations
			"Indicator system"
			- @IndicatorsSystem indicatorsSystem not nullable cascade="none" databaseColumn="INDICATORS_SYSTEM_FK" <-> versions;
			"All children"
			- Bag<@ElementLevel> childrenAllLevels cascade="all-delete-orphan" fetch="lazy" inverse orderby="parent asc, orderInLevel asc" <-> indicatorsSystemVersion;
			"Children at first level"
			- Bag<@ElementLevel> childrenFirstLevel cascade="all-delete-orphan" fetch="lazy" inverse orderby="orderInLevel asc" <-> indicatorsSystemVersionFirstLevel;
		
			Repository IndicatorsSystemVersionRepository {
				@IndicatorsSystemVersion save(@IndicatorsSystemVersion entity);
				delete;
            	protected List<@IndicatorsSystemVersion> findByQuery;
            	protected List<@IndicatorsSystemVersion> findByQuery(String query, Map<String, Object> parameters, int maxResult);
            	@IndicatorsSystemVersion retrieveIndicatorsSystemVersion(String uuid, String versionNumber);
            	List<@IndicatorsSystemVersion> findIndicatorsSystemVersions(String uriGopestat, @IndicatorsSystemStateEnum state);
			}
		} 
		
		"Level at indicators system"
        Entity ElementLevel {
        	databaseTable = "TBL_ELEMENTS_LEVELS"
        	hint="idSequence=ELEMENTS_LEVELS"
        	gap
        	!auditable
        	
            "Order in level"
			Long orderInLevel not nullable;
			
			// Relations
			"Children"
			- Bag<@ElementLevel> children cascade="all-delete-orphan" inverse orderby="orderInLevel asc" <-> parent;
            "Parent"
            - @ElementLevel parent cascade="none" databaseColumn="PARENT_FK" nullable <-> children;
			"Dimension"
			- @Dimension dimension cascade="all" nullable databaseColumn="DIMENSION_FK" <-> elementLevel;
			"Indicator instance"
			- @IndicatorInstance indicatorInstance cascade="all" nullable databaseColumn="INDICATOR_INSTANCE_FK" <-> elementLevel;
            "Indicators system version"
			- @IndicatorsSystemVersion indicatorsSystemVersion cascade="none" fetch="lazy" not nullable databaseColumn="IND_SYSTEM_VERSION_ALL_FK" <-> childrenAllLevels;
			"Indicators system version"
			- @IndicatorsSystemVersion indicatorsSystemVersionFirstLevel cascade="none" fetch="lazy" nullable databaseColumn="IND_SYSTEM_VERSION_FIRST_FK" <-> childrenFirstLevel;
			
			"Provides access to ElementLevel"
            Repository ElementLevelRepository {
            	@ElementLevel save(@ElementLevel entity);
            	delete;
            }
        }  		
		
        "Dimension of indicators system"
        Entity Dimension {
        	databaseTable = "TBL_DIMENSIONS"
        	hint="idSequence=DIMENSIONS"
        	
            "Title"
            - @InternationalString title cascade="all" not nullable databaseColumn="TITLE_FK";

			// Relations
            "Element level"
            - @ElementLevel elementLevel cascade="none" not nullable inverse <-> dimension;
			            
			"Provides access to Dimension"
            Repository DimensionRepository {
            	@Dimension save(@Dimension entity);
            	protected List<@Dimension> findByQuery(String query, Map<String, Object> parameters, int maxResult);
            	@Dimension findDimension(String uuid);
            }
        }  		
        
		"Instance of indicator"
        Entity IndicatorInstance {
        	databaseTable = "TBL_INDICATORS_INSTANCES"
        	hint="idSequence=INDICATORS_INSTANCES"
        	       		
			"Title"
       		- @InternationalString title cascade="all" not nullable databaseColumn="TITLE_FK";
        	"Temporary granularity"
        	String temporaryGranularityId nullable;
        	"Temporary value"
        	String temporaryValue nullable;
        	"Geographic granularity"
			String geographicGranularityId nullable;
        	"Geographic value"
			String geographicValue nullable;
			
			// Relations
            "Element level"
            - @ElementLevel elementLevel cascade="none" not nullable inverse <-> indicatorInstance;
        	"Indicator"
			- @Indicator indicator cascade="none" not nullable databaseColumn="INDICATOR_FK" <-> indicatorsInstances;
            
			"Provides access to IndicatorInstance"
            Repository IndicatorInstanceRepository {
            	@IndicatorInstance save(@IndicatorInstance entity);
            	protected List<@IndicatorInstance> findByQuery(String query, Map<String, Object> parameters, int maxResult);
            	@IndicatorInstance findIndicatorInstance(String uuid);
            	Boolean existAnyIndicatorInstance(String indicatorsSystemUuid, String indicatorsSystemVersionNumber);
            }
        }         
		
        BasicType IndicatorsSystemVersionInformation {
        	gap
        	"Id into database of IndicatorsSystemVersion"
    		Long idIndicatorsSystemVersion nullable databaseColumn="ID";
    		"Version number of IndicatorsSystemVersion"
    		String versionNumber nullable databaseColumn="VERSION_NUMBER" length="10";
    	}	
    	
		"Indicator entity"
		Entity Indicator {
			databaseTable="TBL_INDICATORS"
			hint="idSequence=INDICATORS"
			gap

			"Code. Non modifiable"
			String code not nullable;
			
			// Can not do an OneToOne relation with IndicatorVersion because this produces a CyclicDependency with Indicator
			"Version in production"
            - @IndicatorVersionInformation productionVersion nullable databaseColumn="PRODUCTION";
            "Version in diffusion"
            - @IndicatorVersionInformation diffusionVersion nullable databaseColumn="DIFFUSION";
			"All versions"
			- Bag<@IndicatorVersion> versions cascade="all-delete-orphan" fetch="lazy" inverse <-> indicator orderby="versionNumber asc";
			"Indicator instances"
			- Bag<@IndicatorInstance> indicatorsInstances cascade="none" fetch="lazy" inverse <-> indicator;
			
			Repository IndicatorRepository {
		   		@Indicator save(@Indicator entity);
		   		delete;
            	protected List<@Indicator> findByQuery;
            	protected List<@Indicator> findByQuery(String query, Map<String, Object> parameters, int maxResult);
            	@Indicator retrieveIndicator(String uuid);
            	List<@Indicator> findIndicators(String code);
			}
		}
	    
		"Version of indicator entity"
		Entity IndicatorVersion {
			databaseTable="TBL_INDICATORS_VERSIONS"
			hint="idSequence=INDICATORS_VERSIONS"
			
			"State"
			- @IndicatorStateEnum state not nullable;
			String versionNumber not nullable length="10";
       		"Name"
       		- @InternationalString name cascade="all" not nullable databaseColumn="NAME_FK";
       		"Acronym"
       		- @InternationalString acronym cascade="all" nullable databaseColumn="ACRONYM_FK";
       		"Subject code"
			String subjectCode nullable;
			"Commentary"
       		- @InternationalString commentary cascade="all" nullable databaseColumn="COMMENTARY_FK";
			"Notes"
       		- @InternationalString notes  cascade="all" nullable databaseColumn="NOTES_FK";
       		"Note url"
       		String noteUrl nullable;
       		
			"Date when the indicator was sended to production validation"
			DateTimeTZ productionValidationDate nullable;
			"User who sended to production validation"
			String productionValidationUser nullable;
			"Date when the indicator was sended to diffusion validation"
			DateTimeTZ diffusionValidationDate nullable;
			"User who sended to diffusion validation"
			String diffusionValidationUser nullable;
			"Date when the indicator was published"
			DateTimeTZ publicationDate nullable;
			"User who published"
			String publicationUser nullable;
			"Date when the indicator was archived"
			DateTimeTZ archiveDate nullable;
			"User who archived"
			String archiveUser nullable;
					
			// Relations
			"Indicator"
			- @Indicator indicator not nullable cascade="none" databaseColumn="INDICATOR_FK" <-> versions;
			"Data sources"
			- Bag<@DataSource> dataSources cascade="all-delete-orphan" fetch="lazy" inverse orderby="id asc" <-> indicatorVersion;
		
			Repository IndicatorVersionRepository {
				@IndicatorVersion save(@IndicatorVersion entity);
				delete;
            	protected List<@IndicatorVersion> findByQuery;
            	protected List<@IndicatorVersion> findByQuery(String query, Map<String, Object> parameters, int maxResult);
            	@IndicatorVersion retrieveIndicatorVersion(String uuid, String versionNumber);
            	List<@IndicatorVersion> findIndicatorsVersions(@IndicatorStateEnum state);
			}
		}     	
		
        BasicType IndicatorVersionInformation {
        	gap
        	"Id into database of IndicatorVersion"
    		Long idIndicatorVersion nullable databaseColumn="ID";
    		"Version number of IndicatorVersion"
    		String versionNumber nullable databaseColumn="VERSION_NUMBER" length="10";
    	}			
    	
		"Data source of indicator"
        Entity DataSource {
        	databaseTable = "TBL_DATA_SOURCES"
        	hint="idSequence=DATA_SOURCES"
        	
			"Query in GPE"
			String queryGpe not nullable;
			"Px"
			String px not nullable;
			"Temporary variable"
			String temporaryVariable not nullable;
			"Geographic variable"
			String geographicVariable not nullable;
			"Other variables fixing one category"
			- Bag<@DataSourceVariable> otherVariables cascade="all-delete-orphan" fetch="lazy" inverse orderby="id asc" databaseColumn="DATA_SOURCE_FK";
			
			// Relations
            "Indicator version"
			- @IndicatorVersion indicatorVersion cascade="none" fetch="lazy" nullable databaseColumn="INDICATOR_VERSION_FK" <-> dataSources;
            
			"Provides access to Dimension"
            Repository DataSourceRepository {
            	@DataSource save(@DataSource entity);
            	delete;
            	protected List<@DataSource> findByQuery(String query, Map<String, Object> parameters, int maxResult);
            	@DataSource findDataSource(String uuid);
            }
        }  	   

		"Variable of data source"
        Entity DataSourceVariable {
        	databaseTable = "TBL_DATA_SOURCES_VARIABLES"
        	hint="idSequence=DATA_SOURCES_VARIABLES"
        	
			"Variable"
			String variable not nullable;
			"Category"
			String category not nullable;
        }  	    	
	}
	
	// ----------------------------------------------------------------
	// 							ENUMS
	// ----------------------------------------------------------------
	
	Module dtos {    	
		basePackage=es.gobcan.istac.indicators.core.dto
		
		"Dto for indicators system"
	   	DataTransferObject IndicatorsSystemDto extends @AuditDto {
			
			"Unique uuid"
			String uuid;
			"Version number of this indicators system version"	
			String versionNumber;
			"State"
			- @IndicatorsSystemStateEnum state;
			"Semantic identifier of statistic operation"
       		String code;
       		"Uri of statistic operation in Gopestat"
       		String uriGopestat;
       		"Title of statistic operation"
       		- @InternationalStringDto title;
       		"Acronym of statistic operation"
       		- @InternationalStringDto acronym;
       		"Description of statistic operation"
       		- @InternationalStringDto description;
       		"Objetive of statistic operation"
       		- @InternationalStringDto objetive;			
       		"Number of version in production"
       		String productionVersion;
       		"Number of version in diffusion"   		
			String diffusionVersion;			
			
			"Date when the indicators system was sended to production validation"
			JAVADATE productionValidationDate;
			"User who sended to production validation"
			String productionValidationUser;
			"Date when the indicators system was sended to diffusion validation"
			JAVADATE diffusionValidationDate;
			"User who sended to diffusion validation"
			String diffusionValidationUser;
			"Date when the indicators system was published"
			JAVADATE publicationDate;
			"User who published"
			String publicationUser;
			"Date when the indicators system was archived"
			JAVADATE archiveDate;
			"User who archived"
			String archiveUser;
	    }
	    
	    "Dto for indicator"
	   	DataTransferObject IndicatorDto extends @AuditDto {
			
			"Unique uuid"
			String uuid;
			"Version number of this indicator version"	
			String versionNumber;
			"Code"
			String code;
			"State"
			- @IndicatorStateEnum state;
       		"Name"
       		- @InternationalStringDto name;
       		"Acronym"
       		- @InternationalStringDto acronym;
       		"Subject code"
			String subjectCode;
			"Commentary"
       		- @InternationalStringDto commentary;
			"Notes"
       		- @InternationalStringDto notes;
       		"Note url"
       		String noteUrl;        
       		
       		"Number of version in production"
       		String productionVersion;
       		"Number of version in diffusion"   		
			String diffusionVersion;			
			
			"Date when the indicators system was sended to production validation"
			JAVADATE productionValidationDate;
			"User who sended to production validation"
			String productionValidationUser;
			"Date when the indicators system was sended to diffusion validation"
			JAVADATE diffusionValidationDate;
			"User who sended to diffusion validation"
			String diffusionValidationUser;
			"Date when publication failed"
			JAVADATE publicationFailedDate;
			"User who publication failed"
			String publicationFailedUser;
			"Date when the indicators system was published"
			JAVADATE publicationDate;
			"User who published"
			String publicationUser;
			"Date when the indicators system was archived"
			JAVADATE archiveDate;
			"User who archived"
			String archiveUser;       		    
	    }
	    
	    "Dto for dimension"
	   	DataTransferObject DimensionDto extends @AuditDto {
			"Unique uuid"
			String uuid;
			"Title"
			- @InternationalStringDto title;

            "Order in level"
			Long orderInLevel;
			"Parent dimension"
			String parentUuid;			
	    }
	    
	    "Dto for data source"
	   	DataTransferObject DataSourceDto extends @AuditDto {
			gap
	   		
			"Unique uuid"
			String uuid;
			"Query in GPE"
			String queryGpe;
			"Px"
			String px;
			"Temporary variable"
			String temporaryVariable;
			"Geographic variable"
			String geographicVariable;
			"Other variables fixing one category"
			- List<@DataSourceVariableDto> otherVariables; 
	    }
	    
	    "Dto for variables and categories"
	   	DataTransferObject DataSourceVariableDto {
	   		"Variable"
			String variable;
			"Category fixed"
			String category;
	    }
	    
	    "Dto for indicator instance"
	   	DataTransferObject IndicatorInstanceDto extends @AuditDto {
			"Unique uuid"
			String uuid;
			"Title"
			- @InternationalStringDto title;
			"Indicator"
			String indicatorUuid;
        	"Temporary granularity"
        	String temporaryGranularityId;
        	"Temporary value"
        	String temporaryValue;
        	"Geographic granularity"
			String geographicGranularityId;
        	"Geographic value"
			String geographicValue;
			
            "Order in level"
			Long orderInLevel;
			"Parent dimension"
			String parentUuid;
	    }
	    
	    "AuditDto"
    	DataTransferObject AuditDto {
       		JAVADATE createdDate;
       		String createdBy;
       		JAVADATE lastUpdated;
       		String lastUpdatedBy;
    	}
    	
    	"Dto for structure of indicators system"
	   	DataTransferObject IndicatorsSystemStructureDto {
	   		"Uuid"
	   		String uuid;
			"Version number of this indicator version"	
			String versionNumber;
			"Elements: dimensions and indicators instances"
	   		- List<@ElementLevelDto> elements;
	   	}
	   	"Dto for element of level. Can be a dimension or an indicator instance"
	   	DataTransferObject ElementLevelDto {
	   		gap
	   		
	   		"Dimension"
	   		- @DimensionDto dimension;
	   		"Indicator instance"
	   		- @IndicatorInstanceDto indicatorInstance;
	   		"Subelements (only when this element is a dimension)"
	   		- List<@ElementLevelDto> subelements;
	   	}
	}
	
	// ----------------------------------------------------------------
	// 							ENUMS
	// ----------------------------------------------------------------
	
	Module enums {    	
		basePackage=es.gobcan.istac.indicators.core.enume

		"States of indicators system"
    	enum IndicatorsSystemStateEnum {
    		DRAFT,
    		PRODUCTION_VALIDATION,
    		DIFFUSION_VALIDATION,
    		VALIDATION_REJECTED,
    		PUBLISHED,
    		ARCHIVED
    	}      
		"States of indicator"
    	enum IndicatorStateEnum {
    		DRAFT,
    		PRODUCTION_VALIDATION,
    		DIFFUSION_VALIDATION,
    		VALIDATION_REJECTED,
    		PUBLICATION_FAILED,    		
    		PUBLISHED,
    		ARCHIVED
    	}	
    	"Type of version of the change"
    	enum VersiontTypeEnum {
    		MAJOR,
    		MINOR
    	}	
	}
}