import "classpath:/btdesign/export/metamac-core-common-entity.btdesign"
import "classpath:/btdesign/metamac-core-common-dtos.btdesign"

Application Indicators {
    basePackage=es.gobcan.istac.indicators
    
    Module core {
		basePackage=es.gobcan.istac.indicators.core
		
		// ----------------------------------------------------------------
		// 							SERVICES
		// ----------------------------------------------------------------     
		
		"Provides access to indicators systems"
    	Service IndicatorsSystemService {
    		> @IndicatorsSystemRepository
    		> @IndicatorsSystemVersionRepository
    		
    		@IndicatorsSystemVersion createIndicatorsSystem(@IndicatorsSystem indicatorsSystem, @IndicatorsSystemVersion indicatorsSystemVersion) throws MetamacException;
	    	@IndicatorsSystemVersion retrieveIndicatorsSystemVersion(String uuid, Long versionNumber) throws MetamacException;
	    	@IndicatorsSystem retrieveIndicatorsSystem(String uuid) throws MetamacException;
	    	updateIndicatorsSystem(@IndicatorsSystem indicatorsSystem) throws MetamacException;
	    	updateIndicatorsSystemVersion(@IndicatorsSystemVersion indicatorsSystemVersion) throws MetamacException;
	    	deleteIndicatorsSystem(String uuid) throws MetamacException;
	    	deleteIndicatorsSystemVersion(String uuid, Long versionNumber) throws MetamacException;
	    	List<@IndicatorsSystem> findIndicatorsSystems(String code) throws MetamacException;
	    	List<@IndicatorsSystemVersion> findIndicatorsSystemVersions(String uuid) throws MetamacException;
    	}
    	
    	"Provides access to indicators systems"
    	Service IndicatorsSystemServiceFacade {
	    	> @IndicatorsSystemService
	    	
	    	"Creates a indicator system in draft"
	    	@IndicatorsSystemDto createIndicatorsSystem(@IndicatorsSystemDto indicatorsSystemDto) throws MetamacException;
	    	"Updates metadata of an indicator system. This version can not be published or archived"
	    	updateIndicatorsSystem(@IndicatorsSystemDto indicatorsSystemDto) throws MetamacException;
	    	"Retrieves an indicator system. If versionNumber is not provided, retrieves last version"
	    	@IndicatorsSystemDto retrieveIndicatorsSystem(String uuid, Long versionNumber) throws MetamacException;
	    	"Retrieves an indicator system published"
	    	@IndicatorsSystemDto retrieveIndicatorsSystemPublished(String uuid) throws MetamacException;
	    	"Deletes a version of an indicator system. Version to remove must be not published nor archived"
	    	deleteIndicatorsSystem(String uuid) throws MetamacException;
	    	"Sends indicators system to production validation"
	    	sendIndicatorsSystemToProductionValidation(String uuid) throws MetamacException;
	    	"Sends indicators system to diffusion validation"
	    	sendIndicatorsSystemToDiffusionValidation(String uuid) throws MetamacException;
	    	"Refuses validation of indicators system"
	    	refuseIndicatorsSystemValidation(String uuid) throws MetamacException;
	    	"Publishes indicators system"
	    	publishIndicatorsSystem(String uuid) throws MetamacException;	    		    	
	    	"Archives indicators system"
	    	archiveIndicatorsSystem(String uuid) throws MetamacException;	    		    	
	    	"Creates a version on draft of an indicator system in diffusion. Returns new version created"
	    	@IndicatorsSystemDto makeIndicatorsSystemInProduction(@IndicatorsSystemDto indicatorsSystemDto, @IndicatorsSystemVersionEnum versionType) throws MetamacException;
	    }    	

	    // ----------------------------------------------------------------
		// 							ENTITIES
		// ----------------------------------------------------------------        	
		"Indicator system entity"
		Entity IndicatorsSystem {
			databaseTable="TBL_INDICATORS_SYSTEMS"
			hint="idSequence=INDICATORS_SYSTEMS"
			gap
			
			"Semantic identifier of statistic operation. Non modifiable"
       		String code required;
		
			// Can not do an OneToOne relation with IndicatorsSystemVersion because this produces a CyclicDependency with IndicatorsSystem
			"Version in production"
            - @IndicatorsSystemVersionInformation productionVersion nullable databaseColumn="PRODUCTION";
            "Version in difussion"
            - @IndicatorsSystemVersionInformation diffusionVersion nullable databaseColumn="DIFFUSION";
			"All versions"
			- Bag<@IndicatorsSystemVersion> versions cascade="all-delete-orphan" fetch="lazy" inverse <-> indicatorsSystem orderby="versionNumber asc";
			
			
			Repository IndicatorsSystemRepository {
		   		@IndicatorsSystem save(@IndicatorsSystem entity);
		   		delete;
            	protected List<@IndicatorsSystem> findByQuery;
            	@IndicatorsSystem retrieveIndicatorsSystem(String uuid);
            	List<@IndicatorsSystem> findIndicatorsSystems(String code);
			}
		} 
		
		"Version of indicator system entity"
		Entity IndicatorsSystemVersion {
			databaseTable="TBL_INDIC_SYSTEMS_VERSIONS"
			hint="idSequence=INDIC_SYSTEMS_VERSIONS"
			gap
			
			"State"
			- @IndicatorsSystemStateEnum state required;
			Long versionNumber required;
       		"Uri of statistic operation"
       		String uri nullable;
       		"Title of statistic operation"
       		- @InternationalString title cascade="all" databaseColumn="TITLE_FK";
       		"Acronym of statistic operation"
       		- @InternationalString acronym cascade="all" nullable databaseColumn="ACRONYM_FK";
       		"Objetive of statistic operation"
       		- @InternationalString objetive cascade="all" nullable databaseColumn="OBJETIVE_FK";
       		"Description of statistic operation"
       		- @InternationalString description cascade="all" nullable databaseColumn="DESCRIPTION_FK";
       		
			"Date when the indicators system was sended to production validation"
			DateTimeTZ productionValidationDate nullable;
			"User who sended to production validation"
			String productionValidationUser nullable;
			"Date when the indicators system was sended to diffusion validation"
			DateTimeTZ diffusionValidationDate nullable;
			"User who sended to diffusion validation"
			String diffusionValidationUser nullable;
			"Date when the indicators system was published"
			DateTimeTZ publicationDate nullable;
			"User who published"
			String publicationUser nullable;
			"Date when the indicators system was archived"
			DateTimeTZ archiveDate nullable;
			"User who archived"
			String archiveUser nullable;
					
			"Indicator system"
			- @IndicatorsSystem indicatorsSystem not nullable cascade="none" databaseColumn="INDICATORS_SYSTEMS_FK" <-> versions;
		
			Repository IndicatorsSystemVersionRepository {
				@IndicatorsSystemVersion save(@IndicatorsSystemVersion entity);
				delete;
            	protected List<@IndicatorsSystemVersion> findByQuery;
            	@IndicatorsSystemVersion retrieveIndicatorsSystemVersion(String uuid, Long versionNumber);
            	List<@IndicatorsSystemVersion> findIndicatorsSystemVersions(String uri);
			}
		} 
		
        BasicType IndicatorsSystemVersionInformation {
        	gap
        	"Id into database of IndicatorsSystemVersion"
    		Long idIndicatorsSystemVersion nullable databaseColumn="ID";
    		"Version number of IndicatorsSystemVersion"
    		Long versionNumber nullable databaseColumn="VERSION_NUMBER";
    	}	
	}
	
	// ----------------------------------------------------------------
	// 							ENUMS
	// ----------------------------------------------------------------
	
	Module dtos {    	
		basePackage=es.gobcan.istac.indicators.core.dto
		
		"Dto for indicators system"
	   	DataTransferObject IndicatorsSystemDto extends @AuditableDto {
			
			"Unique uuid"
			String uuid;
			"Version number of this indicator system version"	
			Long versionNumber;
			"State"
			- @IndicatorsSystemStateEnum state;
			"Semantic identifier of statistic operation"
       		String code;
       		"Uri of statistic operation"
       		String uri;
       		"Title of statistic operation"
       		- @InternationalStringDto title;
       		"Acronym of statistic operation"
       		- @InternationalStringDto acronym;
       		"Description of statistic operation"
       		- @InternationalStringDto description;
       		"Objetive of statistic operation"
       		- @InternationalStringDto objetive;			
       		"Number of version in production"
       		Long productionVersion;
       		"Number of version in diffusion"   		
			Long diffusionVersion;			
			
			"Date when the indicators system was sended to production validation"
			JAVADATE productionValidationDate;
			"User who sended to production validation"
			String productionValidationUser;
			"Date when the indicators system was sended to diffusion validation"
			JAVADATE diffusionValidationDate;
			"User who sended to diffusion validation"
			String diffusionValidationUser;
			"Date when the indicators system was published"
			JAVADATE publicationDate;
			"User who published"
			String publicationUser;
			"Date when the indicators system was archived"
			JAVADATE archiveDate;
			"User who archived"
			String archiveUser;
	    }
	    
	    "Dto for indicator"
	   	DataTransferObject IndicatorDto extends @AuditableDto {
			
			"Unique uuid"
			String uuid;
			"Version number of this indicator version"	
			Long versionNumber;
			"Code"
			String code;
			"State"
			- @IndicatorStateEnum state;
       		"Name"
       		- @InternationalStringDto name;
       		"Acronym"
       		- @InternationalStringDto acronym;
       		"Subject code"
			String subjectCode;
			"Comment"
       		- @InternationalStringDto comment;
			"Notes"
       		- @InternationalStringDto notes;
       		"Note url"
       		String noteUrl;            
	    }
	}
	
	// ----------------------------------------------------------------
	// 							ENUMS
	// ----------------------------------------------------------------
	
	Module enums {    	
		basePackage=es.gobcan.istac.indicators.core.enume

		"States of indicator system"
    	enum IndicatorsSystemStateEnum {
    		DRAFT,
    		PRODUCTION_VALIDATION,
    		DIFFUSION_VALIDATION,
    		PUBLISHED,
    		ARCHIVED
    	}      
		"States of indicator"
    	enum IndicatorStateEnum {
    		DRAFT,
    		PRODUCTION_VALIDATION,
    		DIFFUSION_VALIDATION,
    		PUBLISHED,
    		ARCHIVED
    	}	
    	"Type of version of the change"
    	enum IndicatorsSystemVersionEnum {
    		MAYOR,
    		MINOR
    	}	
	}
}